plugins {
	id 'application'
	id 'checkstyle'
	id 'jacoco'
	id 'org.springframework.boot' version '2.5.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.adarshr.test-logger' version '2.1.1'
	id 'com.github.ben-manes.versions' version '0.38.0'
	id 'org.liquibase.gradle' version '2.0.4'
	// Плагины для swagger
	// Позволяют получить сгенерированную документацию по API в виде json файла
	id 'org.springdoc.openapi-gradle-plugin' version '1.3.3'
	id 'com.github.johnrengelman.processes' version '0.5.0'
	// Сборщик фронтенда
	id 'org.siouan.frontend-jdk11' version '6.0.0'
}

group = 'hexlet.code'
version = '1.0-SNAPSHOT'
sourceCompatibility = '17'

application {
	mainClass = "hexlet.code.AppApplication"
}

repositories {
	mavenCentral()
}

dependencies {
	compileOnly 'org.projectlombok:lombok:1.18.22'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	implementation (
		'org.springframework.boot:spring-boot-starter-data-jpa',
		'org.springframework.boot:spring-boot-starter-web',
		'org.liquibase:liquibase-core',
	)

	liquibaseRuntime(
			sourceSets.main.output,
			'org.liquibase:liquibase-core:4.6.1',
			'org.liquibase.ext:liquibase-hibernate5:4.17.2',
			'org.springframework.boot:spring-boot-starter-data-jpa',
	)

	runtimeOnly(
			'com.h2database:h2:1.4.200',
			'org.postgresql:postgresql:42.5.0'
	)

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

}

diffChangeLog {
	dependsOn compileJava
}

liquibase {
	activities {
		main {
			changeLogFile 'src/main/resources/db/changelog/changelog-master.xml'
			url 'jdbc:h2:./taskManagerDev'
			username ''
			password ''
			referenceUrl 'hibernate:spring:hexlet.code.model' +
					'?dialect=org.hibernate.dialect.H2Dialect' +
					'&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy'
		}
	}
}

test {
	useJUnitPlatform()
}

testlogger {
	showStandardStreams true
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
	}
}

jacoco {
	toolVersion = "0.8.8"
}

tasks.named('test') {
	useJUnitPlatform()
}

checkstyle {
	toolVersion "10.0"
}

task stage(dependsOn: [clean, installDist])
installDist.mustRunAfter clean
